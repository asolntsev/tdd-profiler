apply plugin: 'java'

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
java {
  sourceCompatibility = 17
  targetCompatibility = 17
}

group='com.jokerconf'
version='1.0-SNAPSHOT'

defaultTasks 'test', 'jar'

sourceSets {
  main {java {srcDir 'src'}}
  test {java {srcDir 'test'}}
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.javassist:javassist:3.30.2-GA'
  testImplementation 'org.mockito:mockito-core:5.22.0'
  testImplementation 'com.codeborne:selenide:7.14.0'
  testImplementation "org.assertj:assertj-core:3.27.7"

  testImplementation platform("org.junit:junit-bom:6.0.3")
  testImplementation "org.junit.jupiter:junit-jupiter"
  testImplementation "org.junit.platform:junit-platform-engine"
  testImplementation "org.junit.platform:junit-platform-launcher"
}

test {
  include 'profiler/**/*'
}

tasks.withType(Test).configureEach { testTask ->
  useJUnitPlatform()
  jvmArgs("--add-opens", "java.base/java.lang=ALL-UNNAMED")
  testTask.systemProperties['file.encoding'] = 'UTF-8'
  testTask.systemProperties['user.country'] = 'TR'
  testTask.systemProperties['user.language'] = 'tr'
  testTask.testLogging { exceptionFormat = 'full' }
  testTask.outputs.upToDateWhen { false }
}

jar {
  // archiveName = "${rootProject.name}-${rootProject.version}.jar"
  manifest {
    attributes(
      'Premain-Class': 'profiler.Agent',
      'Can-Redefine-Classes': 'true',
      'Can-Retransform-Classes': 'true',
      'Can-Set-Native-Method-Prefix': 'true',
      'Implementation-Title': project.group + '.' + project.name,
      'Implementation-Version': version,
      'Implementation-Vendor': "Codeborne"
    )
  }
}
